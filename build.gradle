buildscript {
    ext {
        springBootVersion = '2.6.0-M3'
        spotbugsVersion = '4.4.1'
        lombokVersion = '1.18.20'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE'
        classpath 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.0'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group 'moe.pine.profile'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-Xlint:unchecked'
        options.compilerArgs << '-Xlint:deprecation'
    }

    test {
        useJUnitPlatform()
    }

    // ----- JaCoCo ---------------------------------------

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.8.7'
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency 'javax.servlet:javax.servlet-api:4.0.1'
            dependency "org.projectlombok:lombok:$lombokVersion"

            // Guava
            dependency 'com.google.guava:guava:30.1.1-jre'

            // Apache Commons
            dependency 'org.apache.commons:commons-lang3:3.12.0'
            dependency 'org.apache.commons:commons-collections4:4.4'

            // HTML Compressor
            dependency 'com.googlecode.htmlcompressor:htmlcompressor:1.5.2'
            dependency 'org.mozilla:rhino:1.7.10'

            // Utilities
            dependency 'moe.pine:spring-cache-interceptors:0.2.0'

            // Test
            dependency 'org.powermock:powermock-reflect:2.0.0'
            dependency "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
        }
    }
    dependencies {
        // Lombok
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
    }


    // ----- SpotBugs -------------------------------------

    apply plugin: 'com.github.spotbugs'

    spotbugs {
        toolVersion = spotbugsVersion
        excludeFilter = file("${project.rootDir}/config/findbugs-exclude.xml")
        spotbugsTest.enabled = false
    }
    spotbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}


// ----- JaCoCo -------------------------------------------

task jacocoMerge(type: JacocoMerge) {
    dependsOn(subprojects.jacocoTestReport.dependsOn)
    mustRunAfter(subprojects.jacocoTestReport.mustRunAfter)
    destinationFile = file("${buildDir}/jacoco/test.exec")
    executionData = files(subprojects.jacocoTestReport.executionData)
            .filter { f -> f.exists() }
}

jacocoTestReport {
    dependsOn(jacocoMerge)
    reports {
        xml.enabled = true
        html.enabled = true
    }

    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
}


// ----- Build Time Tracker -------------------------------

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
            barstyle 'ascii'
        }
    }
}
